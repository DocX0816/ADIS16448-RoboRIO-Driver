plugins {
  id 'cpp'
  id 'java'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.1'
  id 'edu.wpi.first.NativeUtils' version '2020.1.5'
  id 'edu.wpi.first.GradleJni' version '0.9.1'
  id 'edu.wpi.first.GradleVsCode' version '0.9.6'
  id 'google-test-test-suite'
}

allprojects {
    repositories {
        mavenCentral()
    }
    if (project.hasProperty('releaseMode')) {
        wpilibRepositories.addAllReleaseRepositories(it)
    } else {
        wpilibRepositories.addAllDevelopmentRepositories(it)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['java/src/main/java']
        }
        resources {
            srcDirs = ['java/src/main/resources']
        }
    }
}

jar.baseName = "ADIS16448_IMU"

apply from: 'config.gradle'

apply from: 'dependencies.gradle'

nativeUtils {
  exportsConfigs {
      adis16448imu {
      }
  }
}

model {
  components {
    adis16448imu(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'c++/src/main/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'c++/src/main/include'
          }
        }
      }


      nativeUtils.useRequiredLibrary(it, "wpilib_shared")
    }
  }

  testSuites {
    adis16448imuTest(GoogleTestTestSuiteSpec) {
      testing $.components.adis16448imu
      sources.cpp {
        source {
          srcDir 'c++/src/test/cpp'
          include '**/*.cpp'
        }
      }
      nativeUtils.useRequiredLibrary(it, "wpilib_shared", "googletest_static")
    }
  }

}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '5.4.1'
}
